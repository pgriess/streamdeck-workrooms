#!/usr/bin/env osascript
#
# Get browser state from any active calls. Returns MUTED, ACTIVE, or UNKNOWN by
# writing to stdout.
#
# NOTES:
#
#   * Chrome only
#
#   * User must enable this in Chrome via the menu View->Developer->Apple
#     JavaScript from Apple Events

on run argv
    set binDir to do shell script "dirname " & (quoted form of ((POSIX path of (path to me)) as string))
    set queryText to (read file (POSIX file (binDir & "/query.js"))) as string

    tell application "Google Chrome"
        repeat with w in windows
            repeat with t in tabs of w
                # TODO: Handle multiple tabs with calls. Only one should be active.
                if (offset of "/LINK:" in (URL of t as string)) is not 0 then
                    tell t
                        return execute javascript ("(" & queryText & ")();")
                    end tell
                end if
            end repeat
        end repeat
    end tell

    # No rooms
    #
    # XXX: We return the same number of elements as the JavaScript does. This is
    #      pretty fragile. Maybe we should be smarter in parsing the output of
    #      this script in stead. If we return 1 element, for example, elements 2
    #      and above don't update.
    return "NONE NONE NONE"
end run