#!/usr/bin/env osascript
#
# Get browser state from any active calls. Returns MUTED, ACTIVE, or UNKNOWN by
# writing to stdout.
#
# NOTES:
#
#   * Chrome only
#
#   * User must enable this in Chrome via the menu View->Developer->Apple
#     JavaScript from Apple Events

on run argv
    set binDir to do shell script "dirname " & (quoted form of ((POSIX path of (path to me)) as string))
    set queryText to (read file (POSIX file (binDir & "/query.js"))) as string

    set target to (item 1 of argv)

    tell application "Google Chrome"
        repeat with w in windows
            repeat with t in tabs of w
                # TODO: Handle multiple tabs with calls. Only one should be active.
                if title of t is "Workplace Room" then
                    tell t
                        return execute javascript ("(" & queryText & ")('" & target & "');")
                    end tell
                end if
            end repeat
        end repeat
    end tell

    # No rooms
    return "UNKNOWN"
end run